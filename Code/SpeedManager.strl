module SpeedManager:

input CruiseState : integer;
input QuickDecel, QuickAccel;
input On;
input Set;
input Speed : float;

output CruiseSpeed : float;

constant SpeedMax = 150.0f : float;
constant SpeedMin = 30.0f : float;
constant SpeedInc = 2.5f : float;


var setToCruiseSpeed := 0.0f : float in
loop
    emit CruiseSpeed(setToCruiseSpeed);
    pause;
    trap T in 
        if (?CruiseState = 1) then
            setToCruiseSpeed := 0.0f;
        else % CC enabled
            present(Set or On) then % could cause errors because On should not function same as set?
                if (?Speed < SpeedMin) then
                    setToCruiseSpeed := SpeedMin;
                elsif (?Speed > SpeedMax) then
                    setToCruiseSpeed := SpeedMax;
                else 
                    setToCruiseSpeed := ?Speed;
                end if;
            end present;

            present(QuickAccel) then
                if (setToCruiseSpeed + SpeedInc <= SpeedMax) then
                    setToCruiseSpeed := setToCruiseSpeed + SpeedInc;
                else 
                    setToCruiseSpeed := SpeedMax;
                end if;
                exit T;
            end present;

            present(QuickDecel) then
                if (setToCruiseSpeed - SpeedInc >= SpeedMin) then
                    setToCruiseSpeed := setToCruiseSpeed - SpeedInc;
                else 
                    setToCruiseSpeed := SpeedMin;
                end if;
                exit T;
            end present;
        end if;
    end trap
end loop
end var
end module